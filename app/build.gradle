plugins {
    id 'com.android.application'
    id 'com.google.dagger.hilt.android' version '2.50'
    id 'com.google.devtools.ksp' version '1.9.23-1.0.20'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'jacoco'
}

def apikeyPropertiesFile = rootProject.file("config.properties")
def apikeyProperties = new Properties()

if (apikeyPropertiesFile.exists()) {
    apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))
}

def versionPropsFile = rootProject.file('version.properties')
def versionProps = new Properties()
versionProps.load(new FileInputStream(versionPropsFile))

android {
    namespace 'com.raylabs.laundryhub'
    compileSdkVersion 36
    defaultConfig {
        applicationId "com.raylabs.laundryhub"
        minSdkVersion 23
        targetSdk 36
        versionCode versionProps['VERSION_CODE'].toInteger()
        versionName versionProps['VERSION_NAME']
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables {
            useSupportLibrary = true
        }

        buildConfigField("String", "SPREAD_SHEET_ID", "\"${apikeyProperties["SPREAD_SHEET_ID"] as String}\"")
        buildConfigField("String", "WEB_CLIENT_ID", "\"${apikeyProperties["WEB_CLIENT_ID"] as String}\"")
    }

    signingConfigs {
        release {
            storeFile file(apikeyProperties["MYAPP_RELEASE_KEYSTORE_FILE"])
            storePassword apikeyProperties["MYAPP_RELEASE_KEYSTORE_PASSWORD"]
            keyAlias apikeyProperties["MYAPP_RELEASE_KEY_ALIAS"]
            keyPassword apikeyProperties["MYAPP_RELEASE_KEY_PASSWORD"]
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            shrinkResources true
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }

    buildFeatures {
        compose true
        buildConfig true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.13"
    }

    kotlinOptions {
        jvmTarget = "18"
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
            excludes += 'META-INF/INDEX.LIST'
            excludes += 'META-INF/DEPENDENCIES'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_18
        targetCompatibility JavaVersion.VERSION_18
    }

    hilt {
        enableAggregatingTask = false
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
}

// Ensure unit tests always produce coverage data (CI friendly)
tasks.withType(Test).configureEach {
    jacoco.includeNoLocationClasses = false
    jacoco.excludes = ['jdk.internal.*', 'jdk.proxy*', 'java.*', 'sun.*', 'com.sun.*']
}

tasks.register("jacocoTestReport", JacocoReport) {
    dependsOn "testDebugUnitTest"

    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }

    // === Pola exclude: generated + UI layer ===
    def excludes = [
            // generated/boilerplate
            '**/R.class', '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*$ViewInjector*.*',
            '**/*$ViewBinder*.*',
            '**/androidx/**/*.*',

            // UI yang mau kamu singkirkan dari coverage
            '**/*Activity.*', '**/*Activity$*.*',
            '**/*Fragment.*', '**/*Fragment$*.*',
            '**/*Adapter.*',  '**/*Adapter$*.*',
            '**/com/raylabs/laundryhub/ui/**',
            '**/com/raylabs/laundryhub/databinding/**',
            '**/com/raylabs/laundryhub/data/source/local/room/**',
            '**/com/raylabs/laundryhub/LaundryHubApp*'
    ]

    // === Cari file .class di seluruh buildDir (lebih toleran untuk variasi AGP/Gradle di CI) ===
    def b = layout.buildDirectory.get().asFile

    // 1) Semua direktori kelas yang umum untuk Kotlin/Java debug
    def classDirTrees = [
            fileTree(dir: new File(b, 'tmp/kotlin-classes/debug'), exclude: excludes),
            fileTree(dir: new File(b, 'intermediates/javac/debug'), exclude: excludes),
            fileTree(dir: new File(b, 'intermediates/javac/debug/classes'), exclude: excludes),
            fileTree(dir: new File(b, 'intermediates/classes/debug'), exclude: excludes)
    ].findAll { it.dir.exists() }

    def allTrees = []
    allTrees.addAll(classDirTrees)
    // Do not add unit test output to avoid duplicate classes (e.g., Application)

    // Gunakan from(...) langsung agar Gradle tidak mengosongkan koleksi FileTree
    classDirectories.from(allTrees)

    // === Collect execution data for debug unit tests ===
    def execFiles = files(
            fileTree(dir: new File(b, 'jacoco'), include: ['testDebugUnitTest.exec']),
            fileTree(dir: new File(b, 'outputs/unit_test_code_coverage/debugUnitTest'), include: ['**/*.ec'])
    )
    executionData.from(execFiles)

    // Debug log
    doFirst {
        println "[jacoco] buildDir = ${b.absolutePath}"

        println "[jacoco] class dir trees:"
        classDirTrees.each { println " - DIR tree: ${it.dir} (exists=${it.dir.exists()}) files=${it.files.size()}" }

        println "[jacoco] allTrees size = ${allTrees.size()} (dirTrees=${classDirTrees.size()})"
        def total = 0
        allTrees.each { ft -> total += ft.files.size() }
        println "[jacoco] class files included = ${total}"

        println "[jacoco] executionData files:"
        execFiles.each { println " - EXEC: ${it.absolutePath} (exists=${it.exists()})" }
    }
}


jacoco {
    toolVersion = "0.8.11"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //Need To Upgrade Gradle Plugin if we need to update the library
    implementation 'androidx.core:core-ktx:1.17.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'com.google.android.material:material:1.13.0'

    //SplashScreen
    implementation "androidx.core:core-splashscreen:1.0.1"

    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.8.6"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.6'
    implementation "androidx.lifecycle:lifecycle-runtime-compose:2.8.6"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.6"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.8.6"

    //Coil
    implementation "io.coil-kt:coil-compose:2.5.0"

    //lottie
    implementation "com.airbnb.android:lottie-compose:6.1.0"

    //Compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.navigation:navigation-compose:$nav_version"
    implementation "androidx.compose.material:material:1.6.8"
    implementation "androidx.activity:activity-compose:1.10.1"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"

    // Firebase
    implementation(platform("com.google.firebase:firebase-bom:31.0.0"))
    implementation("com.google.firebase:firebase-auth-ktx")
    implementation("com.google.firebase:firebase-auth")
    implementation("com.firebaseui:firebase-ui-auth:7.2.0")
    implementation("com.google.firebase:firebase-crashlytics-ktx")
    implementation("com.google.firebase:firebase-appdistribution-api:16.0.0-beta17")


    // Google API Client & Sheets
    implementation 'com.google.api-client:google-api-client-android:1.35.0'
    implementation 'com.google.api-client:google-api-client-gson:1.35.0'
    implementation 'com.google.http-client:google-http-client-android:1.41.5'
    implementation "com.google.apis:google-api-services-sheets:v4-rev20250415-2.0.0"
    implementation 'com.google.android.gms:play-services-auth:21.3.0'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.35.0'
    implementation 'com.google.code.gson:gson:2.11.0'

    // Hilt
    implementation 'com.google.dagger:hilt-android:2.50'
    ksp 'com.google.dagger:hilt-compiler:2.50'
    implementation "androidx.hilt:hilt-navigation-compose:1.2.0"

    // Coroutine
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.1"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.8.1"
    // atau versi yang sesuai

    //Data store
    /* v1.5.0 not support booleanPreferencesKey (?) */
    implementation "androidx.datastore:datastore-preferences:1.1.2"

    testImplementation 'org.mockito:mockito-core:5.2.0'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:5.2.0'
    testImplementation 'org.mockito:mockito-inline:5.2.0'
    implementation "androidx.compose.material3:material3:1.4.0"
}
